apply plugin: 'com.android.application'


def appBaseVersionCode = 1
def appVersionName = "1.0"

def makeVersionCode = { densityName ->
    def densityCodes = [all: 1, mdpi: 2, hdpi: 3, xhdpi: 4, xxhdpi: 5, xxxhdpi: 6]

    def key = densityName == null ? "all" : densityName
    return densityCodes.get(key) * 1_000_000 + appBaseVersionCode
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "fr.tvbarthel.experiment.splitdensitybug"
        minSdkVersion 16
        targetSdkVersion 25
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs 'en'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'density'

    productFlavors {

        universal {
            dimension 'density'
            versionCode makeVersionCode()
        }

        mdpi {
            dimension 'density'
            resConfigs 'mdpi'
            versionCode makeVersionCode('mdpi')
        }

        hdpi {
            dimension 'density'
            resConfigs 'hdpi'
            versionCode makeVersionCode('hdpi')
        }

        xhdpi {
            dimension 'density'
            resConfigs 'xhdpi'
            versionCode makeVersionCode('xhdpi')
        }

        xxhdpi {
            dimension 'density'
            resConfigs 'xxhdpi'
            versionCode makeVersionCode('xxhdpi')
        }

        xxxhdpi {
            dimension 'density'
            resConfigs 'xxxhdpi'
            versionCode makeVersionCode('xxxhdpi')
        }

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    testCompile 'junit:junit:4.12'
}